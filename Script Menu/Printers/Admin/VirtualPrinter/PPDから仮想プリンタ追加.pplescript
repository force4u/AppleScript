#!/usr/bin/env osascript
----+----1----+----2----+-----3----+----4----+----5----+----6----+----7
#com.cocolog-nifty.quicktimer.icefloe
----+----1----+----2----+-----3----+----4----+----5----+----6----+----7
use AppleScript version "2.8"
use framework "Foundation"
use scripting additions

property refMe : a reference to current application

set objFileManager to refMe's NSFileManager's defaultManager()

###URL
set strPrinterURL to "ipp://localhost/"
###場所等　日本語可
set strLocation to "PostScriptファイル書出用の仮想プリンタです"

###################################
#####入力フォルダ
###################################
###ダイアログのデフォルト
set listDirectoryArray to (objFileManager's URLsForDirectory:(refMe's NSLibraryDirectory) inDomains:(refMe's NSLocalDomainMask))
set ocidDirFilePathURL to listDirectoryArray's objectAtIndex:0
set ocidPrinterPpdDirPathURL to ocidDirFilePathURL's URLByAppendingPathComponent:"Printers/PPDs/Contents/Resources/" isDirectory:true
set aliasDefaultLocation to ocidPrinterPpdDirPathURL as alias

set listChooseFileUTI to {"public.item"}
set strPromptText to "PPDファイルを選んでください" as text
set listAliasFilePath to (choose file with prompt strPromptText default location (aliasDefaultLocation) of type listChooseFileUTI with invisibles and multiple selections allowed without showing package contents) as list



###################################
#####Printerフォルダ
###################################
set listDirectoryArray to (objFileManager's URLsForDirectory:(refMe's NSLibraryDirectory) inDomains:(refMe's NSUserDomainMask))
set ocidDirFilePathURL to listDirectoryArray's objectAtIndex:0
set ocidPrinterPpdDirPathURL to ocidDirFilePathURL's URLByAppendingPathComponent:"Printers" isDirectory:true
set ocidAttrDict to refMe's NSMutableDictionary's alloc()'s initWithCapacity:0
ocidAttrDict's setValue:511 forKey:(refMe's NSFilePosixPermissions)
objFileManager's createDirectoryAtURL:ocidPrinterPpdDirPathURL withIntermediateDirectories:true attributes:ocidAttrDict |error|:(reference)


###################################
#####選んだPPDの数だけ繰り返し
###################################

repeat with objAliasFilePath in listAliasFilePath
	set strFilePath to POSIX path of objAliasFilePath as text
	set ocidFilePathStr to (refMe's NSString's stringWithString:strFilePath)
	set ocidFilePath to ocidFilePathStr's stringByStandardizingPath()
	set ocidFilePathURL to (refMe's NSURL's alloc()'s initFileURLWithPath:ocidFilePath)
	set strFilePathURL to ocidFilePathURL's |path|() as text
	set ocidBaseFilePathURL to ocidFilePathURL's URLByDeletingPathExtension()
	set strExtension to ocidFilePathURL's pathExtension() as text
	###################################
	#####PPDを読み取って
	###################################
	if strExtension is "gz" then
		set strCommandText to "/bin/cat \"" & strFilePathURL & "\" | /usr/bin/zcat" as text
		set strCatData to (do shell script strCommandText) as text
		set ocidPpdsData to (refMe's NSString's stringWithString:strCatData)
		set ocidPpdsDataArray to (ocidPpdsData's componentsSeparatedByString:"\r")
	else
		set strCommandText to "/bin/cat \"" & strFilePathURL & "\"" as text
		set strCatData to (do shell script strCommandText) as text
		set ocidPpdsData to (refMe's NSString's stringWithString:strCatData)
		set ocidPpdsDataArray to (ocidPpdsData's componentsSeparatedByString:"\r")
	end if
	###################################
	#####モデルの名前等を取得
	###################################	
	repeat with itemArray in ocidPpdsDataArray
		set strItemArray to itemArray as text
		if strItemArray starts with "*ModelName" then
			set ocidItemArray to (itemArray's componentsSeparatedByString:"\"")
			set strName to (ocidItemArray's objectAtIndex:1) as text
			set strDescription to strName
		else if strItemArray starts with "*NickName" then
			set ocidItemArray to (itemArray's componentsSeparatedByString:"\"")
			set strName to (ocidItemArray's objectAtIndex:1) as text
			set strDescription to strName
		else if strItemArray starts with "* ShortNickName" then
			set ocidItemArray to (itemArray's componentsSeparatedByString:"\"")
			set strName to (ocidItemArray's objectAtIndex:1) as text
			set strDescription to strName
		end if
	end repeat
	###################################
	#####ファイル名のスペース取ってデバイス名に
	###################################
	set coidFileName to (refMe's NSMutableString's alloc()'s initWithCapacity:0)
	(coidFileName's setString:((ocidBaseFilePathURL's lastPathComponent()) as text))
	set ocidStrRange to (coidFileName's rangeOfString:coidFileName)
	(coidFileName's replaceOccurrencesOfString:"\\s" withString:"" options:(refMe's NSRegularExpressionSearch) range:ocidStrRange)
	set strPrinterName to coidFileName as text
	###################################
	#####取得した情報をもとにプリンタ作成
	###################################
	try
		set strCommandText to "lpadmin -p \"" & strPrinterName & "\" -E -v \"" & strPrinterURL & "\" -P \"" & strFilePathURL & "\" -D \"" & strDescription & "\" -L \"" & strLocation & "\" -o printer-is-shared=false" as text
		do shell script strCommandText
	end try
	###################################
	#####仮想プリンタなので停止させる
	###################################
	try
		set theComandText to ("/usr/sbin/cupsdisable   \"" & strPrinterName & "\"") as text
		do shell script theComandText
	end try
	
end repeat

###################################
#####システム設定を開く
###################################

set ocidWorkspace to refMe's NSWorkspace's sharedWorkspace()

set ocidSystemPreferencesURL to refMe's NSURL's URLWithString:"x-apple.systempreferences:com.apple.Print-Scan-Settings.extension"
ocidWorkspace's openURL:ocidSystemPreferencesURL



